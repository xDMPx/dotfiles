# Maintainer: dr460nf1r3 <dr460nf1r3 at garudalinux dot org>
# Contributor: Philip MÃ¼ller <philm[at]garuda[dog]org>
# Contributor: artoo <artoo@garuda.org>
# Contributor: anex <assassin.anex[@]gmail.com>

_repo=garuda-tools-livecd

pkgbase=garuda-live
pkgname=('garuda-live-base'
	'garuda-live-systemd'
	'garuda-live-skel'
	'garuda-live-portable-efi')
pkgver=r3.19f1cf0
pkgrel=1
pkgdesc='garuda Linux live session'
arch=('any')
url="https://gitlab.com/garuda-linux/tools/garuda-tools-livecd"
license=('GPL')
makedepends=('git')
source=("git+$url.git")
sha256sums=('SKIP')

pkgver() {
	cd "${srcdir}"/garuda-tools-livecd || exit
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	cd "${srcdir}"/${_repo} || exit #-${pkgver}
	make PREFIX=/usr SYSCONFDIR=/etc
}

package_garuda-live-base() {
	pkgdesc='Garuda Linux live base scripts'
	depends=('garuda-tools-base>=0.13')
	conflicts=('garuda-livecd-base')
	replaces=('garuda-livecd-base')

	cd "${srcdir}"/${_repo} || exit #-${pkgver}
	make PREFIX=/usr SYSCONFDIR=/etc DESTDIR="${pkgdir}" install_base
}

package_garuda-live-systemd() {
	pkgdesc='Garuda Linux live Systemd units'
	depends=('systemd' 'garuda-live-base' 'reflector')
	conflicts=('garuda-livecd-systemd')
	# 	replaces=('garuda-livecd-systemd')

	cd "${srcdir}"/${_repo} || exit #-${pkgver}
	make PREFIX=/usr SYSCONFDIR=/etc DESTDIR="${pkgdir}" install_sd
}

package_garuda-live-skel() {
	pkgdesc='Garuda Linux live session autostart items'

	cd "${srcdir}"/${_repo} || exit #-${pkgver}
	make PREFIX=/usr SYSCONFDIR=/etc DESTDIR="${pkgdir}" install_xdg
}

package_garuda-live-portable-efi() {
	pkgdesc='Garuda Linux live session portable EFI settings'
	depends=('grub')

	cd "${srcdir}"/${_repo} || exit #-${pkgver}
	make PREFIX=/usr SYSCONFDIR=/etc DESTDIR="${pkgdir}" install_portable_efi
}
