# Maintainer: dr460nf1r3 <dr460nf1r3 at garudalinux dot org>
# Contributor: Philip MÃ¼ller <philm[at]manjaro[dot]org>
# Contributor: artoo <artoo@manjaro.org>
# Contributor: anex <assassin.anex[@]gmail.com>
# Contributor: Stefano Capitani <stefano@manjaro.org>
# Contributor: Matti Hyttinen <matti@manjaro.org>

pkgbase=grub-theme-garuda
pkgname=('grub-theme-garuda-live-common' 'grub-theme-garuda-live' 'grub-theme-garuda')
pkgver=r26.972b17c
pkgrel=2
pkgdesc='Garuda Linux GRUB theme'
arch=('any')
url="https://gitlab.com/garuda-linux/themes-and-settings/grub-theme-garuda"
license=('GPL')
makedepends=('git')
source=("grub-theme::git+$url.git")
sha256sums=('SKIP')

pkgver() {
	cd "${srcdir}/grub-theme" || exit
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package_grub-theme-garuda-live-common() {
	depends=('grub')

	cd grub-theme || exit
	make PREFIX=/usr DESTDIR="${pkgdir}" install_common
}
package_grub-theme-garuda-live() {
	depends=('grub-theme-garuda-live-common')

	cd grub-theme || exit
	make PREFIX=/usr DESTDIR="${pkgdir}" install_garuda
}

package_grub-theme-garuda() {
	depends=('grub')
	install=garuda-theme.install

	cd grub-theme/garuda-live || exit
	sed -i -e 's,.*text = "Welcome to Garuda".*,#text = "Welcome to Garuda",' theme.txt
	find . -type f -exec install -D -m644 {} "${pkgdir}"/usr/share/grub/themes/garuda/{} \;
}
