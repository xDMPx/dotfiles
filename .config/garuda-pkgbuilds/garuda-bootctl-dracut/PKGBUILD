# Maintainer: dr460nf1r3 <dr460nf1r3 at garudalinux dot org>
# Contributor: Librewish <librewish@gmail.com>

pkgname=garuda-bootctl-dracut
pkgdesc="Systemd-boot, Dracut and Secure Boot support for Garuda Linux"
pkgver=0.0.5
pkgrel=1
arch=('any')
license=('GPL')
depends=('pacman' 'systemd' 'dracut' 'sbsigntools' 'sbctl' 'tpm2-tss' 'tpm2-tools' 'dracut-hook-uefi')
provides=('mkinitpcio' 'grub' 'grub-btrfs')
backup=(etc/dracut.conf.d/dracut-garuda.conf)
install="$pkgname".install
source=(bootctl-update.hook
	dracut-garuda.conf
	gen-dracut-cmdline)
sha256sums=('a16de3b405780f5e63818597b73d93095f6ddd89f162b8750fe4438c1d0d4461'
	'2603c1e33ccb9455228909e9838ce69ec46955801b548e3cc4922102498b8266'
	'911c45c0e3fb5fc8e6afe2cdfd489750e26f310e80337de8216bd64d44f81118')

package() {
	cd "$srcdir" || exit
	install -d "$pkgdir/usr/share/libalpm/hooks"
	install -Dm644 bootctl-update.hook "$pkgdir/usr/share/libalpm/hooks/bootctl-update.hook"

	install -d "$pkgdir/etc/dracut.conf.d"
	install -Dm644 dracut-garuda.conf "$pkgdir/etc/dracut.conf.d/dracut-garuda.conf"

	install -d "$pkgdir/usr/bin"
	install -Dm755 gen-dracut-cmdline "$pkgdir/usr/bin/gen-dracut-cmdline"
}
